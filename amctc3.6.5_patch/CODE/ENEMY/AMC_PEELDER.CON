// ************************************************************************************
// PARADIGM ESCHATON ELDER
// ************************************************************************************

action APE_ELDER_FROZE 0 1 5 1 10
action APE_ELDER_GROW 31 1 1 1 10
action APE_ELDER_IDLE 0 1 5 1 20
action APE_ELDERRUN  0 4 5 1 11
action APE_ELDERFIRE 20 2 5 1 12
action APE_ELDERFLINCH 30 1 1 1 10
action APE_ELDERDYING 30 5 1 1 16
action APE_ELDERDEAD 34 1 1 1 10

action APE_ELDER_HEADSHOT 35 6 1 1 16
action APE_ELDER_HEADDEAD 41 1 1 1 10

move PEELDERWALKVELS 120
move PEELDERRUNVELS 160 
move PEELDERSTOP 0

ai AIPE_ELDERSEEKENEMY APE_ELDERRUN PEELDERWALKVELS seekplayer
ai AIPE_ELDER_OBFUSCATE APE_ELDERRUN PEELDERWALKVELS seekplayer
ai AIPE_ELDERSHOOTENEMY APE_ELDERFIRE PEELDERSTOP faceplayer
ai AIPE_ELDERSHOOT_BOTENEMY APE_ELDERFIRE PEELDERSTOP shoottarget
ai AIPE_ELDERDEAD APE_ELDERDEAD PEELDERSTOP faceplayer
ai AIPE_ELDER_HEADDEAD APE_ELDER_HEADDEAD PEELDERSTOP faceplayer
ai AIPE_ELDERSHRUNK APE_ELDERRUN PEELDERRUNVELS furthestdir
ai AIPE_ELDER_GROW APE_ELDER_GROW STOP faceplayer

action APE_DUP_WANDER -2 4 5 1 11

defstate PE_ELDER_STARTOBFUSCATE
	ifrnd 8
		ifpdistg 8192
		{
			spawn BIG_SMOKE2
			sound UNMAKERF
			set CLOAK_COUNT 1
			espawn 18768
			set THISSHADOW RETURN
			seta[].cstat 514
			seta[].blend 128
			ai AIPE_ELDER_OBFUSCATE
		}
ends


useractor notenemy 18768
fall
	ifaction 0
	{
	action APE_DUP_WANDER
	randvar TEMP 2048
	seta[].ang TEMP
	move PEELDERWALKVELS seekplayer
	sizeat 24 24
	set TEMP5 0
	strength 10
	cstat 256
	}

ifaction APE_DUP_WANDER 
{ 
ifrnd 4 move PEELDERWALKVELS randomangle
else move PEELDERWALKVELS seekplayer
ifhitweapon set TEMP5 1 
}

ife TEMP5 1
{
	ifl TEMP4 254 add TEMP4 8
	set alpha TEMP4
	state SPRITE_FADE
	ifg TEMP4 254 
		{
		setactorvar[myowner].CLOAK_COUNT 0
		espawn BIG_SMOKE2 geta[RETURN].z TEMP sub TEMP 4096 seta[RETURN].z TEMP
		killit
		}
}

enda

useractor enemystayput PE_ELDER_STAYPUT 175 APE_ELDER_IDLE
state monst_glow
cactor PE_ELDER
enda

useractor enemy PE_ELDER 175 APE_ELDER_IDLE
fall

ifai 0
{
	state monst_glow
	spawn SHOOTME
	ifspritepal 24 { strength 300 sizeat 26 26 } else
	{ strength 125 sizeat 24 24 }
	set TEMP9 5
	spriteflags NORMAL_ENEMY
	set INTERNALCOUNT 0
	action APE_ELDERRUN
	ai AIPE_ELDERSEEKENEMY
	cstator 257
}

state ENEMYKNOCKBACKS
state enemyfloordamage

//state BODYA_DAMAGE_MODIFIER

ifaction APE_ELDER_FROZE state frozen_code 
else
ifaction APE_ELDERFLINCH
{
	sub PAIN_AMOUNT 1
	move PEELDERSTOP
	ife PAIN_AMOUNT 0 ai AIPE_ELDERSEEKENEMY
}
else
ifai AIPE_ELDERDEAD
{
move PEELDERSTOP
    ifhitweapon
	    {
	      ifwasweapon RADIUSEXPLOSION
	      {
	        state squish_sounds
			state standard_jibs
			state human_jibs
	        killit
	      }
	      break
	    }
}
else
ifai AIPE_ELDER_HEADDEAD
{
move PEELDERSTOP
    ifhitweapon
	    {
	      ifwasweapon RADIUSEXPLOSION
	      {
	        state squish_sounds
			state standard_jibs
			state human_jibs
	        killit
	      }
	      break
	    }
}
else
ifaction APE_ELDERDYING
{
	ifactioncount 5 { state BODY_FALL_NOISES spawn BLOODPOOL ai AIPE_ELDERDEAD }
}
else
ifaction APE_ELDER_HEADSHOT
{
	ifactioncount 6 { state BODY_FALL_NOISES spawn BLOODPOOL ai AIPE_ELDER_HEADDEAD }
}
else
ifai AIPE_ELDER_GROW state genericgrowcode
else
ifai AIPE_ELDERSHRUNK
{
    ifcount SHRUNKDONECOUNT
      ai AIPE_ELDERSEEKENEMY
    else ifcount SHRUNKCOUNT
	  {
	  sound ACTOR_UNSHRINK
      sizeto 34 34
	  }
    else
      state genericshrunkcode
    break
}
else
ifai AIPE_ELDER_OBFUSCATE
{
	ifpdistl 4096 set CLOAK_COUNT 0 // disengage ghost mode 
	
	ife CLOAK_COUNT 0
	{
		sound UNMADE
		spawn BIG_SMOKE2
		setactorvar[THISSHADOW].TEMP5 1
		ifrnd 24 sound PE_SEAS else ifrnd 24 sound PE_SIMPLE
		seta[].cstat 257
		seta[].blend 0
		
		state SKILL_LEVELADJUST
		ifl RANDOM_CHANCE SKILLCHANCE
			ai AIPE_ELDERSEEKENEMY
		else
			ai AIPE_ELDERSHOOT_BOTENEMY
	}
}
else
ifai AIPE_ELDERSEEKENEMY
{	
ifg ALLY_MAG 0 sub ALLY_MAG 1

	ifg TEMP9 0
	 ifg INTERNALCOUNT 5
		{
		sub TEMP9 1
		soundonce HEAT_SINK_EJECT
		shoot HEAT_SINK
		set INTERNALCOUNT 0
		}

		
ifbulletnear ifrnd 16 soundonce PE_LOOKOUT
	
	ifn target -1 
	 ife ALLY_MAG 0
		{
		state SKILL_SHOOT_LEVELADJUST
		ifl RANDOM_CHANCE SKILLCHANCE ai AIPE_ELDERSHOOT_BOTENEMY
		}
	
	
	else ife target -1 ifrnd 32 { ai AIPE_ELDERSEEKENEMY }
	
	// Control when the Elder turns invisible
	ifcansee
		ifcanseetarget
			ifrnd 128
				nullop // High chance of not doing anything if player has line of sight
			else
				state PE_ELDER_STARTOBFUSCATE
			
		else
			state PE_ELDER_STARTOBFUSCATE
	
	ifrnd 4
		{
		ifcansee nullop else
			{
			ifrnd 8 soundonce PE_STAYALERT
			else ifrnd 8 soundonce PE_SHOWYOURSELF
			}
		}
}
else
ifai AIPE_ELDERSHOOT_BOTENEMY
{
	ifactioncount 1
	ifn target -1
	{ 
	ifg INTERNALCOUNT 5 add ALLY_MAG 30 else add ALLY_MAG 10
	add INTERNALCOUNT 1
	state shoot_target
	
			soundonce MC_MESONC_FIRE
			mulvar TEMP3 2
	
			getactorvar[target].IS_PLAYER TEMP2
			ife TEMP2 1
			{
			setprojectile[RUBYBLAST].extra_rand 30
			eshoot RUBYBLAST
			setprojectile[RUBYBLAST].extra_rand 50
			}
			else
			{
			setprojectile[RUBYBLAST].extra_rand 30
			ezshoot TEMP3 RUBYBLAST
			setprojectile[RUBYBLAST].extra_rand 50
			}

		ife PERFMODE 1
		 ifcansee
			{
			set gunsmoke_z 8244 
			set gunsmoke_angle -64
			state spawn_gunsmoke_z
			set gunsmoke_z 5344
			espawn 16113
			seta[RETURN].pal 21
			state spawn_muzzleflash
			}
		ai AIPE_ELDERSEEKENEMY
		resetactioncount
	}
	else ife target -1 { ifrnd 32 soundonce PE_HAHAHA ai AIPE_ELDERSEEKENEMY }
}
else
ifai AIPE_ELDERSHOOTENEMY
{
	ifactioncount 2
	{ 
	set ALLY_MAG 10
			soundonce MC_MESONC_FIRE
			setprojectile[RUBYBLAST].extra_rand 30
			eshoot RUBYBLAST
			setprojectile[RUBYBLAST].extra_rand 50
		ife PERFMODE 1
		 ifcansee
			{
			set gunsmoke_z 8244 
			set gunsmoke_angle -64
			state spawn_gunsmoke_z
			set gunsmoke_z 5344
			espawn 16113
			state spawn_muzzleflash
			}
		ifg ALLY_MAG 10 ai AIPE_ELDERSEEKENEMY
		ifrnd 32 ai AIPE_ELDERSEEKENEMY
		resetactioncount
	}
}

ifhitweapon
{
	state NEWGUNEFFECTS
	//spawn BLOOD
	//guts JIBS6 1	
	state random_wall_jibs
	ifdead
		{
		ifand ENEMY_KILLED 32 nullop else xorvar ENEMY_KILLED 32
		addkills 1
		state thegames_score
		  ifwasweapon RADIUSEXPLOSION
		  {
			state squish_sounds
			state standard_jibs
		  state human_jibs
		  killit
		  }
		else
		  ifwasweapon RPG
		  {
			state squish_sounds
			state standard_jibs
		  state human_jibs
		  killit
		  }
		  else
		ifwasweapon GROWSPARK { cstat 0 sound ACTOR_GROWING ai AIPE_ELDER_GROW break }
		else ifg ICE_DAMAGE 0 { spritepal 1 strength 1 sound SOMETHINGFROZE action APE_ELDER_FROZE } 
		else
			{
			state rf
			ife HEADSHOT 2 
			{
			    ifg FIRE_DAMAGE 0 set FIRE_DAMAGE 0
				ifrnd 32 state trigger_showoff
				state jib_sounds
				ifrnd 128 sound HEL_HS1 else sound HEL_HS2
				shoot NJIB 
				shoot NJIB 
				guts JIBS6 3 
				guts JIBS2 2 
				action APE_ELDER_HEADSHOT
			}
			else ifg SPIRIT_DAMAGE 0
			{
			shoot SPARK2 shoot SPARK2 shoot SPARK2 
			spritepal 1
			guts JIBS3 6
			spawn SPIRIT_DEATH_GUY
			killit
			}
			else
			ifg ENERGY_DAMAGE 0
				{
				shoot SPARK2 shoot SPARK2 shoot SPARK2 
				spritepal 4
				guts JIBS3 6
				spawn DISINTIGRATE_GUY
				killit
				}
			else
				{
				ifrnd 128 sound PE_PAIN1 else sound PE_PAIN2
				action APE_ELDERDYING
				}
			ifrnd 32 ifg TEMP9 0 spawn HEAT_SINK_PACK
			}
		ifphealthl 50
			{
			state SKILL_LEVELADJUST
			ifl RANDOM_CHANCE SKILLCHANCE spawn HYPOSPRAY
			}
		}
else
	ifg PAIN_AMOUNT 0 action APE_ELDERFLINCH 
	else ifrnd 16 { state PAIN_SKILL_LEVELADJUST action APE_ELDERFLINCH }
	else ifwasweapon SHRINKSPARK { sound ACTOR_SHRINKING ai AIPE_ELDERSHRUNK }
}

state checksquished

enda